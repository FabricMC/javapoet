plugins {
    id 'java'
    id 'maven-publish'
}

sourceCompatibility = 17
targetCompatibility = 17

group 'net.fabricmc'
archivesBaseName = 'javapoet'

def ENV = System.getenv()
version '0.1.0' + (ENV.GITHUB_ACTIONS ? "" : "+local")

repositories {
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    mavenCentral()
}

dependencies {
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation 'com.google.testing.compile:compile-testing:0.19'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.jimfs:jimfs:1.2'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.eclipse.jdt.core.compiler:ecj:4.6.1'
    
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        mavenLocal()

        if (ENV.MAVEN_URL) {
            repositories.maven {
                name "fabric"
                url ENV.MAVEN_URL
                credentials {
                    username ENV.MAVEN_USERNAME
                    password ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
    doFirst {
        def xml = new URL("https://maven.fabricmc.net/net/fabricmc/javapoet/maven-metadata.xml").text
        def metadata = new XmlSlurper().parseText(xml)
        def versions = metadata.versioning.versions.version*.text();
        if (versions.contains(version)) {
            throw new RuntimeException("${version} has already been released!")
        }
    }
}

publish.mustRunAfter checkVersion
